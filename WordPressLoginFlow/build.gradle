plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.automattic.android.publish-to-s3"
}

ext {
    kotlin_ktx_version = '1.3.2'
    daggerVersion = '2.42'
    appCompatVersion = '1.0.2'
}

repositories {
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.fluxc"
            includeGroup "org.wordpress.wellsql"
        }
    }
    mavenCentral()
    google()
    jcenter()
    maven { url "https://www.jitpack.io" }
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        vectorDrawables.useSupportLibrary = true
    }
}

dependencies {
    implementation ('org.wordpress:utils:1.26') {
        exclude group: "com.android.volley"
    }

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.google.android.material:material:1.2.1"

    implementation "androidx.core:core-ktx:$kotlin_ktx_version"

    api 'com.google.android.gms:play-services-auth:18.1.0'

    implementation("org.wordpress:fluxc:trunk-1436f46fc296ede0e65e117bbfced38fa34cec6d") {
        exclude group: "com.android.support"
        exclude group: "org.wordpress", module: "utils"
    }

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    lintChecks 'org.wordpress:lint:1.1.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'org.assertj:assertj-core:3.11.1'
}

// Add properties named "wp.xxx" to our BuildConfig
android.buildTypes.all { buildType ->
    Properties gradleProperties = new Properties()
    File propertiesFile = file("../gradle.properties")
    if (propertiesFile.exists()) {
        gradleProperties.load(new FileInputStream(propertiesFile))
    } else {
        // Load defaults
        gradleProperties.load(new FileInputStream(file("../gradle.properties-example")))
    }
    gradleProperties.any { property ->
        if (property.key.toLowerCase().startsWith("wp.")) {
            buildType.buildConfigField "String", property.key.replace("wp.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
        if (property.key.toLowerCase().startsWith("wp.res.")) {
            buildType.resValue "string", property.key.replace("wp.res.", "").replace(".", "_").toLowerCase(),
                    "${property.value}"
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            LoginPublication(MavenPublication) {
                from components.release

                groupId "org.wordpress"
                artifactId "login"
                artifact tasks.named("androidSourcesJar")
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}
