import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.library' apply false
    id 'org.jetbrains.kotlin.android' apply false
    id "com.autonomousapps.dependency-analysis"
}

ext {
    minSdkVersion = 24
    compileSdkVersion = 34
    targetSdkVersion = 34
}

ext {
    // libs
    wordpressLintVersion = '2.1.0'
    wordpressUtilsVersion = '3.5.0'
    wordpressFluxCVersion = '3079-0f189a0307b77a315df19c38292f8d5a86e3f0d5'
    gravatarSdkVersion = '0.2.0'

    // main
    androidxAppCompatVersion = '1.6.1'
    androidxCoreVersion = '1.12.0'
    androidxConstraintLayoutVersion = '2.0.4'
    androidxGlidLayoutVersion = '1.0.0'
    androidxLegacySupportV13Version = '1.0.0'
    androidxMediaVersion = '1.2.1'
    androidxVectorDrawableAnimatedVersion = '1.1.0'
    daggerVersion = '2.47'
    glideVersion = '4.12.0'
    javaxAnnotationVersion = '10.0-b28'
    materialVersion = '1.2.1'
    playServicesAuthVersion = '18.1.0'
    credentialManagerVersion = '1.2.0'

    // test
    androidxArchCoreVersion = '2.1.0'
    assertjVersion = '3.11.1'
    junitVersion = '4.13.2'
    mockitoVersion = '2.28.2'
    robolectricVersion = '4.9'
}

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android/jcenter-mirror"
            content {
                includeVersion "com.android.volley", "volley", "1.1.1"
            }
        }
    }

    if (tasks.findByPath('checkstyle') == null) {
        tasks.create(name: 'checkstyle', type: Checkstyle) {
            source 'src'

            classpath = files()
        }

        checkstyle {
            toolVersion = '8.3'
            configFile file("${project.rootDir}/config/checkstyle.xml")
        }
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }
}
